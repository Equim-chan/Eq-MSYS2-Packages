# Maintainer: Equim <sayaka@ekyu.moe>

pkgname=cmatrix
pkgver=1.2a
pkgrel=12
pkgdesc="A curses-based scrolling 'Matrix'-like screen"
arch=('i686' 'x86_64')
url="http://www.asty.org/cmatrix/"
license=('GPL2')
depends=('ncurses')
makedepends=('ncurses-devel')
source=("http://www.asty.org/$pkgname/dist/$pkgname-$pkgver.tar.gz")
sha256sums=('1fa6e6caea254b6fe70a492efddc1b40ad7ccb950a5adfd80df75b640577064c')

build() {
  cd "$pkgname-$pkgver"

  CPPFLAGS="-D_FORTIFY_SOURCE=0" ./configure \
    --prefix=/usr --mandir=/usr/share/man

  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  for i in AUTHORS NEWS COPYING README ChangeLog TODO INSTALL; do
    install -Dm644 $i "$pkgdir/usr/share/doc/$pkgname/$i"
  done
}
